#!/usr/bin/perl -w
#
# pkgtool -- Tool for generating source/patch files from a source code repository
# 
# Copyright (C) 2000, Michael Jennings
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to
# deal in the Software without restriction, including without limitation the
# rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
# sell copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies of the Software, its documentation and marketing & publicity
# materials, and acknowledgment shall be given in the documentation, materials
# and software packages that this Software was used.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
# THE AUTHORS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
# IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
# CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
#
# $Id: pkgtool,v 1.4 2000/06/20 04:11:25 mej Exp $
#

# Include the Perl Modules we need
use POSIX;
use Getopt::Long;

# Constants
#
# Keep these in sync with the ones in revtool, or
# talk perl into using headers and #define's.
#
# 1-20 are generic errors
sub REVTOOL_NO_SERVER()           {1;}
sub REVTOOL_SERVER_CRASH()        {2;}
sub REVTOOL_BAD_LOGIN()           {3;}
sub REVTOOL_COMMAND_FAILED()      {4;}
sub REVTOOL_ACCESS_DENIED()       {5;}

# 21-40 are retrieval/update errors
sub REVTOOL_FILE_NOT_FOUND()      {21;}
sub REVTOOL_FILE_REMOVED()        {22;}
sub REVTOOL_CONFLICT_FOUND()      {23;}
sub REVTOOL_INVALID_TAG()         {24;}

# 41-60 are add/remove errors
sub REVTOOL_BAD_ADDITION()        {41;}
sub REVTOOL_BAD_REMOVAL()         {42;}

# 61-80 are commit errors
sub REVTOOL_NEED_UPDATE()         {61;}

# 127 is a catch-all
sub REVTOOL_UNSPECIFIED_ERROR()   {127;}


# Return codes for pkgtool
sub PKGTOOL_SUCCESS()           {0;}
sub PKGTOOL_FATAL_ERROR()       {1;}
sub PKGTOOL_TERMINATED()        {50;}
sub PKGTOOL_CRASHED()           {60;}

# Print usage information
sub
print_usage_info
{
    print "\n";
    $leader = "$progname $version Usage Information";
    $underbar = $leader;
    $underbar =~ s/./-/g;
    print "$leader\n$underbar\n";
    print "\n";
    print "  Syntax:   pkgtool [ options ]\n";
    print "\n";
    print "    -h --help                        Show this usage information\n";
    print "    -d --debug                       Turn on debugging\n";
    print "    -v --version                     Show version and copyright\n";
    print "    -b --build                       Build a package\n";
    print "    -x --explode                     Explode (unarchive) a package\n";
    print "    -c --contents                    Check the contents of a package\n";
    print "    -o --sources [srcs]              Specify the sources to generate\n";
    print "    -p --package <file>              Specify the package filename\n";
    print "    -s --specfile --scripts <path>   Specify the spec file (or debian script directory) to use\n";
    print "    -m --module <name>               Specify the module name\n";
    print "    -r --release-tag <tag>           Operate on the revisions starting at <tag>\n";
    print "    -t --head-tag <tag>              Operate on the revisions up to and including <tag>\n";
    print "    -D --dir <directory>             Specify \"directory\" as the full path to the module\n";
    print "    -l --log <logfile>               Specify a log file to send informational output to\n";
    print "\n";
    exit(PKGTOOL_SUCCESS);
}

# Generate timestamp for debugging/log file
sub
get_timestamp
{
    $ts = POSIX::strftime("%A, %d %B %Y %H:%M:%S %Z", localtime);
    return $ts;
}

# Report a fatal error
sub
fatal_error
{
    my $msg = "$progname:  FATAL:  $_[0]";

    print STDERR $msg;
    print $GLOBAL_LOG $msg if ($GLOBAL_LOG);
    exit(PKGTOOL_FATAL_ERROR);
}

# Debugging output
sub
dprintf
{
    return if (! $debug);
    print "[debug:$progname] " if (! $slave);
    printf @_ if (! $slave);
    if ($GLOBAL_LOG) {
        print $GLOBAL_LOG "[debug:$progname] ";
        printf $GLOBAL_LOG @_;
    }
}
sub
dprint
{
    return if (! $debug);
    if (! $slave) {
        print "[debug:$progname] ", @_;
    }
    if ($GLOBAL_LOG) {
        print $GLOBAL_LOG "[debug:$progname] ", @_;
    }
}

# Print an error
sub
eprintf
{
    print "$progname:  Error:  ";
    printf @_;
    if ($GLOBAL_LOG) {
        print $GLOBAL_LOG "$progname:  Error:  ";
        printf $GLOBAL_LOG @_;
    }
}
sub
eprint
{
    print "$progname:  Error:  ", @_;
    if ($GLOBAL_LOG) {
        print $GLOBAL_LOG "$progname:  Error:  ", @_;
    }
}

# Handle a terminate signal
sub
handle_signal
{
    my $sig = $_[0];

    &summarize_failures() if ($#failed_pkgs >= 0);
    eprint "Someone sent me a SIG$sig asking me to exit, so I shall.\n";
    close($GLOBAL_LOG) if ($GLOBAL_LOG);
    exit PKGTOOL_TERMINATED;
}

# Handle a fatal signal
sub
handle_fatal_signal
{
    my $sig = $_[0];

    eprint "Caught fatal signal SIG$sig.  Cleaning up and aborting...\n";
    close($GLOBAL_LOG) if ($GLOBAL_LOG);
    exit PKGTOOL_CRASHED;
}

# Handle a perl warning
sub
handle_warning
{
    dprint @_;
}

# Add a %define
sub
add_define
{
    my ($var, $value) = @_;

    $define{$var} = $value;
    dprint "Added \%define:  $var -> $define{$var}\n";
}

# Replace %define's in a spec file line with their values
sub
replace_defines
{
    my $line = $_[0];

    while ($line =~ /\%(\w+)/g) {
        my $var = $1;

        if (defined $define{$var}) {
            $line =~ s/\%$var/$define{$var}/g;
            reset;
        }
    }
    while ($line =~ /\%\{([^\}]+)\}/g) {
        my $var = $1;

        if (defined $define{$var}) {
            $line =~ s/\%\{$var\}/$define{$var}/g;
            reset;
        }
    }
    return $line;
}

# Parse spec file
sub
parse_spec_file
{
    my $specfile = $_[0];
    my ($line, $oldline, $stage, $pkg);
    local *SPECFILE;

    if (! $specfile) {
        # Look in the specified module
    }

    open(SPECFILE, $specfile) || &fatal_error("Unable to open $specfile -- $!");
    $stage = 0;
    while (<SPECFILE>) {
        chomp($line = $_);
        next if ($line =~ /^\s*\#/ || $line =~ /^\s*$/);
        $oldline = $line;
        $line = &replace_defines($oldline);
        if ($oldline ne $line) {
            dprint "Parsing from $specfile, line $.: \"$oldline\" -> \"$line\"\n";
        } else {
            dprint "Parsing from $specfile, line $.: \"$line\"\n";
        }
        if ($line =~ /^\%(prep|build|install|clean|changelog|trigger|triggerpostun|triggerun|triggerin|verifyscript)\s*$/
            || $line =~ /^\%(package|preun|pre|postun|post|files|description)(\s+\w+)?$/) {
            my $param = $2;

            $stage = $1;
            dprint "Switching to stage \"$stage\"\n";
            if ($stage eq "package" && $param) {
                $pkg = "$packages[0]-$param";
                push @packages, $pkg;
            }
        } elsif ((! $stage) && $line =~ /^\s*(\w+)\s*:\s*(.*)$/) {
            my ($var, $value) = ($1, $2);

            $var =~ tr/[A-Z]/[a-z]/;
            if ($var eq "name") {
                $pkg = $value;
                @packages = ($pkg);
                &add_define("PACKAGE_NAME", $value);
            } elsif ($var =~ /^source(\d*)$/) {
                my $key = ($1 ? $1 : "0");

                $value =~ s/^.*\/([^\/]+)$/$1/;
                $source{$key} = $value;
                &add_define("SOURCE$key", $value);
            } elsif ($var =~ /^patch(\d*)$/) {
                my $key = ($1 ? $1 : "0");

                $value =~ s/^.*\/([^\/]+)$/$1/;
                $patch{$key} = $value;
                &add_define("PATCH$key", $value);
            } else {
                $header{$var} = $value;
                if ($var eq "version") {
                    &add_define("PACKAGE_VERSION", $value);
                } elsif ($var eq "release") {
                    &add_define("PACKAGE_RELEASE", $value);
                }
            }
        } elsif ($line =~ /^%\s*define\s*(\w+)\s*(.*)$/) {
            &add_define($1, $2);
        }
    }
    close(SPECFILE);

    if ($debug) {
        dprint "Got the following sources:\n";
        foreach $src (sort {$a <=> $b} keys %source) {
            dprint "    Source $src -> $source{$src}\n";
        }
        dprint "Got the following patches:\n";
        foreach $p (sort {$a <=> $b} keys %patch) {
            dprint "    Patch $p -> $patch{$p}\n";
        }
        dprint "Got the following header info:\n";
        foreach $h (sort {uc($a) cmp uc($b)} keys %header) {
            dprint "    $h -> $header{$h}\n";
        }
    }
}

# Build RPMS from the spec file
sub
build_rpms
{
    my ($pkg, $specfile) = @_;
    my ($cmd, $rpm, $line, $target);
    my ($err, $msg, $srpm) = (0, 0, 0);
    my (@failed_deps, @not_found, @spec_errors, @out_files);
    local *RPM;

    $rpm = ($pkgs->{$pkg}{RPMCMD} ? $pkgs->{$pkg}{RPMCMD} : "rpm");
    $target = ($pkgs->{$pkg}{ARCH} ? "--target=$pkgs->{$pkg}{ARCH}" : ($main::target ? $main::target : ""));
    $cmd = "$rpm --rcfile \"/usr/lib/rpm/rpmrc:$rpmrc\" $target -ba $specfile";
    dprint "About to run \"$cmd\"\n";
    if (!open(RPM, "$cmd </dev/null 2>&1 |")) {
        return (1, "Execution of \"$cmd\" failed -- $!", undef);
    }
    $err = 0;
    while (<RPM>) {
        chomp($line = $_);
        print "$line\n";
        if ($line =~ /^Wrote:\s+(\S+\.\w+\.rpm)$/) {
            push @out_files, $1;
            if ($line =~ /src\.rpm$/) {
                ($srpm = $line) =~ s/^Wrote:\s+//;
            }
        } elsif ($line =~ /^line \d+: [^:]+: /
                 || $line =~ /^Failed to find \w+:/
                 || $line =~ /^Symlink points to BuildRoot: /) {
            $err = 1;
            push @spec_errors, $line;
        } elsif ($line =~ /^Bad exit status from/) {
            $err = 1;
            $line =~ s/^Bad exit status from \S+ \((%\w+)\)/$1/;
            $msg = "The RPM $line stage exited abnormally";
            last;
        } elsif ($line =~ /^error: failed build dependencies:/) {
            $err = 1;
            while (<RPM>) {
                chomp($line = $_);
                last if ($line !~ /is needed by/);
                $line =~ s/^\s+(\S+)\s+is needed by .*$/$1/;
                push @failed_deps, $line;
            }
            $msg = sprintf("Building this SRPM requires the following packages:  %s", join(" ", @failed_deps));
            last;
        } elsif ($line =~ /^Architecture is not included:/) {
            $err = 1;
            $line =~ s/^Architecture is not included:\s+//;
            $msg = "This SRPM does not build on the $line architecture";
        } elsif ($line =~ /^File (.*): No such file or directory$/
                 || $line =~ /^Bad file: (.*): No such file or directory$/
                 || $line =~ /^File is not a regular file: (.*)$/
                 || $line =~ /^Unable to open icon (\S+):$/
                 || $line =~ /^No (patch number \d+)$/
                 || $line =~ /^Could not open \%files file (\S+): No such file or directory$/
                 || $line =~ /^File not found(?: by glob)?: (.*)$/) {
            $err = 1;
            push @not_found, $1;
        }
    }
    close(RPM);
    dprint "\"$cmd\" returned $?\n";
    if ($? != 0 && $err == 0) {
        $err = 1;
        $msg = "Unhandled RPM build error";
    } elsif ($#not_found != -1) {
        $msg = sprintf("The following were expected by rpm, but no matching files were found:  \"%s\"",
                       join("\", \"", @not_found));
    } elsif ($#spec_errors != -1) {
        $msg = sprintf("The spec file contains the following errors:  \"%s\"", join("\", \"", @spec_errors));
    }

    if ($#out_files >= 0) {
        $pkgs->{$pkg}{OUTFILES} = join(" ", @out_files);
    }
    dprint "build_rpms():  Returning $err, $msg, $srpm\n";
    return ($err, $msg, $srpm);
}

sub
do_compose_stage
{
    my ($specfile, $target, $pn, $dir, $line, $src, $tarball, $cwd);
    my ($err, $msg, $srpm) = (0, 0, 0);
    my (@contents, @tmp, @dirs, @sfiles, @srcs);
    my @p;
    local *DIR;
    local *CMD;

    if ($#_ >= 0) {
        @p = @_;
    } else {
        @p = @packages;
    }

    print "$progname:  Entering the compose stage.  (${\(&get_timestamp())})\n";
    chomp($cwd = `pwd`);

    # No compose stage for package files since they're *already* package files....
    foreach $pkg (grep $pkgs->{$_}{TYPE} eq "module", @p) {
        if ($pkgs->{$pkg}{STAGES} !~ /c/) {
            print "$progname:  Skipping compose stage for the \"$pkg\" package.\n";
            next;
        }
        print "$progname:  Starting compose stage for the \"$pkg\" package....  (${\(&get_timestamp())})\n";

        # The first step is to examine the module directory to find
        # the names of the vendor source files/directories and the spec file.
        if (! chdir("$cwd/$pkgs->{$pkg}{MODULE}")) {
            &fail_package($pkg, "compose", "Could not chdir into $cwd/$pkgs->{$pkg}{MODULE} -- $!");
            next;
        }
        opendir(DIR, ".");
        @contents = readdir(DIR);
        closedir(DIR);
        @tmp = grep($_ =~ /src\.rpm$/, @contents);
        if ($#tmp >= 0) {
            $srpm = $tmp[0];
            if ($#tmp > 0) {
                print "$progname:  Warning:  Multiple target SRPM's found in $pkgs->{$pkg}{MODULE}.\n";
                print "$progname:  Assuming that $srpm is the correct one.\n";
            }
        } else {
            $srpm = 0;
        }
        if ($srpm) {
            # The target SRPM already exists.  Skip this stage.
            print "$progname:  The target SRPM for \"$pkg\" already exists in $cwd/$pkgs->{$pkg}{MODULE}.\n";
            print "$progname:  I'll assume it's the right one.  Compose stage for \"$pkg\" is complete.\n";
            $srpm =~ m/^(\S+)-([^-]+)-([^-]+)\.(?:no)?src\.rpm$/;
            ($pkgs->{$pkg}{VERSION}, $pkgs->{$pkg}{RELEASE}) = ($2, $3);
            $pkgs->{$pkg}{FILENAME} = "$cwd/$pkgs->{$pkg}{MODULE}/$srpm";
            next;
        }

        if ($pkgs->{$pkg}{SPECFILE} && -f $pkgs->{$pkg}{SPECFILE}) {
            # If they gave us the spec file and it exists, use that.
            $specfile = $pkgs->{$pkg}{SPECFILE};
        } else {
            # First choice:  Exactly one file named <pkg>-<distro>.spec
            ($pn = $prodname) =~ s/^va-//;
            @tmp = grep($_ =~ /^($pkg-)?(va-)?$pn\.spec/, @contents);
            if ($#tmp != 0) {
                # Second choice:  Exactly one file named <pkg>.spec
                @tmp = grep($_ =~ /^$pkg\.spec/, @contents);
                if ($#tmp != 0) {
                    # Third choice:  The first match to *.spec*
                    @tmp = grep($_ =~ /\.spec/, @contents);
                }
            }
            $specfile = $tmp[0];
            if (!defined $specfile || $specfile =~ /^$/) {
                # Our attempt to guess failed.  They need to specify the spec file in the .prod file.
                &fail_package($pkg, "compose", "No spec file could be located in $pkgs->{$pkg}{MODULE}");
                next;
            }
        }

        undef @srcs;
        if ($pkgs->{$pkg}{SRCS}) {
            @srcs = split(",", $pkgs->{$pkg}{SRCS});
        } else {
            my $fname;

            foreach $fname (@contents) {
                next if ($fname =~ /^(CVS|SCCS|RCS|BitKeeper)$/);
                next if ($fname =~ /^\./);
                next if ($fname =~ /\.spec(\.in)?$/);
                if (-d $fname) {
                    push @srcs, "$fname:$fname.tar.gz";
                } else {
                    push @srcs, $fname;
                }
            }
        }

        foreach $src (@srcs) {
            my ($src_files, $tarball) = split(":", $src);

            $src_files =~ s/\&/ /g;
            if ($tarball) {
                my $zip;

                if ($pkgs->{$pkg}{TAR}) {
                    $cmd = $pkgs->{$pkg}{TAR};
                    $cmd =~ s/\%f/$src_files/;
                    $cmd =~ s/\%t/$builddir\/SOURCES\/$tarball/;
                } else {
                    if ($pkgs->{$pkg}{ZIP}) {
                        $zip = "--use-compress-program=\"$pkgs->{$pkg}{ZIP}\"";
                    } elsif ($src =~ /(gz|Z)$/) {
                        $zip = "--use-compress-program=gzip";
                    } elsif ($src =~ /\.bz2$/) {
                        $zip = "--use-compress-program=bzip2";
                    }
                    $cmd = "tar --exclude CVS --exclude RCS --exclude BitKeeper --exclude SCCS"
                            . " $zip -cf $builddir/SOURCES/$tarball $src_files";
                }
                dprint "Creating $tarball:  $cmd\n";
                unlink($tarball);
                if (!open(CMD, "$cmd 2>&1 |")) {
                    &fail_package($pkg, "compose", "Execution of \"$cmd\" failed -- $!");
                    $err = 1;
                    last;
                }
                $err = 0;
                while (<CMD>) {
                    chomp($line = $_);
                    print "tar output -> $line\n";
                }
                close(CMD);
                dprint "\"$cmd\" returned $?\n";
                if ($?) {
                    &fail_package($pkg, "compose", "Creation of vendor source tarball $tarball from failed");
                    $err = 1;
                    last;
                }
            } else {
                my $rc;

                $rc = system("cp -f $src_files $builddir/SOURCES/") >> 8;
                if ($rc) {
                    &fail_package($pkg, "compose", "Unable to copy $src_files to $builddir/SOURCES/ -- $!");
                    $err = 1;
                    last;
                }
            }
        }
        if ($err) {
            next;
        }

        # Now build the new SRPM from the new vendor source tarball and the spec file.
        ($err, $msg, $srpm) = &build_rpms($pkg, $specfile);
        dprint "do_compose_stage():  build_rpms($pkg, $specfile) returned $err, $msg, $srpm\n";
        if ($err) {
            &fail_package($pkg, "compose", $msg);
            next;
        }
        dprint "Module apparently just built its SRPM as $srpm\n";

        # Copy it here and assign FILENAME to it.
        system("mv $srpm .");
        $pkgs->{$pkg}{FILENAME} = $srpm;
        $pkgs->{$pkg}{FILENAME} =~ s/^(\S+)\/([^\/]+src\.rpm)$/$2/;
        $pkgs->{$pkg}{FILENAME} =~ m/^(\S+)-([^-]+)-([^-]+)\.(?:no)?src\.rpm$/;
        ($pkgs->{$pkg}{VERSION}, $pkgs->{$pkg}{RELEASE}) = ($2, $3);
        $pkgs->{$pkg}{FILENAME} = "$basedir/$pkgs->{$pkg}{MODULE}/$pkgs->{$pkg}{FILENAME}";
        dprint "Filename is $pkgs->{$pkg}{FILENAME}, version is $pkgs->{$pkg}{VERSION}, release is $pkgs->{$pkg}{RELEASE}\n";

        print "$progname:  Compose stage for \"$pkg\" is complete.  (${\(&get_timestamp())})\n";
    }
    chdir($cwd);
    print "$progname:  Compose stage is now complete.  (${\(&get_timestamp())})\n";
}

sub
do_explode_stage
{
    my ($cmd, $err, $msg, $line, $specfile, $rpm) = undef;
    my @manifest;
    my @p;
    local *RPM;

    if ($#_ >= 0) {
        @p = @_;
    } else {
        @p = @packages;
    }

    print "$progname:  Entering the explode stage.  (${\(&get_timestamp())})\n";

    foreach $pkg (@p) {
        undef @manifest;
        if ($pkgs->{$pkg}{STAGES} !~ /e/ && $pkgs->{$pkg}{STAGES} !~ /x/) {
            print "$progname:  Skipping explode stage for the \"$pkg\" package.\n";
            next;
        }
        print "$progname:  Starting explode stage for the \"$pkg\" package....\n";
        $rpm = ($pkgs->{$pkg}{RPMCMD} ? $pkgs->{$pkg}{RPMCMD} : "rpm");

        # Examine the SRPM and figure out everything it contains.
        $cmd = "$rpm --rcfile $rpmrc -qlp $pkgs->{$pkg}{FILENAME}";
        dprint "About to run \"$cmd\"\n";
        if (!open(RPM, "$cmd 2>&1 |")) {
            &fail_package($pkg, "explode", "Execution of \"$cmd\" failed -- $!");
            next;
        }
        if (defined $pkgs->{$pkg}{SPECFILE}) {
            $specfile = $pkgs->{$pkg}{SPECFILE};
        } else {
            undef $specfile;
        }
        while (<RPM>) {
            chomp($line = $_);
            dprint "RPM contents -> $line\n";
            push @manifest, $line;  # Add the file to the manifest
            if ($line =~ /\.spec(\.in)?$/) {
                if (!defined $specfile) {
                    $specfile = $line;
                    dprint "Found spec file as $specfile\n";
                } elsif ($specfile eq $line) {
                    dprint "Found spec file $specfile as predicted.\n";
                } else {
                    print "$progname:  Warning:  $pkgs->{$pkg}{FILENAME} contains the spec file $line\n";
                    print "$progname:  which is different from the one I was told to expect ($specfile).\n";
                    print "$progname:  I will ignore the auto-detected one as requested, but this may be a problem.\n";
                }
            }
        }
        close(RPM);
        dprint "\"$cmd\" returned $?\n";
        if ($? != 0) {
            # FIXME:  What to do here?
        }
        if (! $specfile) {
            &fail_package($pkg, "explode", "Unable to locate spec file in SRPM");
            next;
        }
        # We'll use these later, so they MUST be defined for
        # any package we plan on actually building later.
        $pkgs->{$pkg}{SPECFILE} = "$builddir/SPECS/$specfile";
        $pkgs->{$pkg}{MANIFEST} = join(" ", @manifest);

        # Check for each of those files.
        $err = 0;
        foreach $piece (@manifest) {
            # FIXME:  Need to handle _topdir for people who did their own rpm* files.
            if (($piece =~ /\.spec(\.in)?$/ && -f "$builddir/SPECS/$piece")
                || (-f "$builddir/SOURCES/$piece")) {
                next;
            }
            $err = 1;  # Something is missing.  We must explode this SRPM.
            last;
        }
        # If everything is there, go on.
        if (! $err) {
            print "$progname:  SRPM has already been completely exploded.\n";
            print "$progname:  Explode stage for \"$pkg\" is complete.\n";
            next;
        }

        # Explode the SRPM into its components
        $cmd = "rpm --rcfile $rpmrc -U $pkgs->{$pkg}{FILENAME}";
        dprint "About to run \"$cmd\"\n";
        if (!open(RPM, "$cmd 2>&1 |")) {
            &fail_package($pkg, "explode", "Execution of \"$cmd\" failed -- $!");
            next;
        }
        $err = 0;
        while (<RPM>) {
            chomp($line = $_);
            print "$line\n";
            if ($line !~ /^$/) {
                # Any output from this command is an error.
                $err = 1;
                $msg = "$line";
                last;
            }
        }
        close(RPM);
        dprint "\"$cmd\" returned $?\n";
        if ($? != 0) {
            # FIXME:  What to do here?
        }
        if ($err) {
            &fail_package($pkg, "explode", $msg);
            next;
        }
        print "$progname:  Explode stage for \"$pkg\" is complete.\n";
    }
    print "$progname:  Explode stage is now complete.  (${\(&get_timestamp())})\n";
}

sub
do_build_stage
{
    my ($cmd, $line, $specfile, $rpm, $tmp, $target) = undef;
    my ($target_age, $orig_age, $spec_age);
    my ($err, $msg, $srpm) = (0, 0, 0);
    my @p;

    if ($#_ >= 0) {
        @p = @_;
    } else {
        @p = @packages;
    }

    print "$progname:  Entering the build stage.  (${\(&get_timestamp())})\n";

    foreach $pkg (@p) {
        if ($pkgs->{$pkg}{STAGES} !~ /b/) {
            print "$progname:  Skipping build stage for the \"$pkg\" package.\n";
            next;
        }
        print "$progname:  Starting build stage for the \"$pkg\" package....  (${\(&get_timestamp())})\n";

        # Check for a target SRPM that is newer than the original SRPM and the
        # spec file.  If such an SRPM exists, skip the build stage for this package.
        $target = "$builddir/SRPMS/$pkg" . "-"
                   . "$pkgs->{$pkg}{VERSION}" . "-" . "$pkgs->{$pkg}{RELEASE}.src.rpm";
        $specfile = $pkgs->{$pkg}{SPECFILE};
        dprint "Target SRPM is $target\n";
        if (-f $target) {
            $target_age = -M _;
            $orig_age = -M $pkgs->{$pkg}{FILENAME};
            $spec_age = -M $specfile;
            dprint "File ages:  $orig_age for $pkgs->{$pkg}{FILENAME}, $target_age for $target,"
                    . " and $spec_age for $specfile\n";
            if ($target_age < $spec_age && $target_age < $orig_age) {
                print "$progname:  The target SRPM already exists.\n";
                print "$progname:  Build stage for \"$pkg\" is complete.\n";
                next;
            }
        }

        # Rebuild from the spec file
        ($err, $msg, $srpm) = &build_rpms($pkg, $specfile);
        dprint "do_build_stage():  build_rpms($pkg, $specfile) returned $err, $msg, $srpm\n";
        if ($err) {
            &fail_package($pkg, "build", $msg);
        }
        print "$progname:  Build stage for \"$pkg\" is complete.  (${\(&get_timestamp())})\n";
    }
    print "$progname:  Build stage is now complete.  (${\(&get_timestamp())})\n";
}

# main() here is basically the same as main() in C
sub
main
{
    # Set up the basic variables
    $progname = "pkgtool";
    $version = "0.1";

    # See the Getopt::Long man page for details on the syntax of this line
    @valid_opts = ("h|help", "v|version", "d|debug", "b|build", "x|explode", "c|contents",
                   "o|sources:s", "p|package=s", "s|specfile|scripts=s", "m|module=s",
                   "r|release-tag=s", "t|head-tag=s", "D|dir=s", "l|log=s");
    Getopt::Long::Configure("no_getopt_compat", "bundling", "no_ignore_case");
    Getopt::Long::GetOptions(@valid_opts);

    # Post-parse the options stuff
    if ($opt_v) {
        # Do not edit this variable.  It is updated automatically by CVS when you commit
        my $rcs_info = 'CVS Revision $Revision: 1.4 $ created on $Date: 2000/06/20 04:11:25 $ by $Author: mej $ ';

        $rcs_info =~ s/\$\s*Revision: (\S+) \$/$1/;
        $rcs_info =~ s/\$\s*Date: (\S+) (\S+) \$/$1 at $2/;
        $rcs_info =~ s/\$\s*Author: (\S+) \$ /$1/;
        print "\n";
	print "$progname $version by Michael Jennings <mej\@eterm.org>\n";
        print "Copyright (c) 2000, Michael Jennings and VA Linux Systems, Inc.\n";
        print "  ($rcs_info)\n";
        print "\n";
	return PKGTOOL_SUCCESS;
    }
    $mode = ($opt_b ? "build" : ($opt_x ? "explode" : ($opt_c ? "contents" : 0)));
    if ($opt_h || (! $mode)) {
	&print_usage_info();   # Never returns
        # This is a really silly way to shut up the "used only once" warnings.
        $opt_h = $opt_v = $opt_b = $opt_c = $opt_x = 0;
    }
    $debug = ($opt_d ? $opt_d : 0);
    $srcs = ($opt_o ? $opt_o : 0);
    $pkg_file = ($opt_p ? $opt_p : 0);
    $specfile = $script_dir = ($opt_s ? $opt_s : 0);
    $module = ($opt_m ? $opt_m : 0);
    $release_tag = ($opt_r ? $opt_r : 0);
    $head_tag = ($opt_t ? $opt_t : 0);
    $GLOBAL_LOG = 0;
    if ($opt_l) {
        if (!open(LOGFILE, ">$opt_l")) {
            eprint "Unable to open $opt_l -- $!\n";
        } else {
            $GLOBAL_LOG = \*LOGFILE;
            system("chattr +S $opt_l");  # Try to set sync on the log file, fail silently
            open(STDERR, ">&LOGFILE");
            select LOGFILE; $| = 1;
        }
    }
    select STDOUT; $| = 1;
    if ($opt_D) {
        $basedir = $opt_D;
    } elsif ($ENV{BASE_DIR} && $ENV{BASE_DIR} =~ /^\//) {
        $basedir = $ENV{BASE_DIR};
    } else {
        chomp($basedir = `pwd`);
    }
    $builddir = ($ENV{BUILD_DIR} ? $ENV{BUILD_DIR} : "$basedir/build");
    $proddir = ($ENV{PROD_DIR} ? $ENV{PROD_DIR} : "$basedir/products");

    # Signal handling
    $SIG{HUP} = 'IGNORE';
    $SIG{INT} = \&handle_signal;
    $SIG{TERM} = \&handle_signal;
    $SIG{QUIT} = \&handle_fatal_signal;
    $SIG{ILL} = \&handle_fatal_signal;
    $SIG{ABRT} = \&handle_fatal_signal;
    $SIG{FPE} = \&handle_fatal_signal;
    $SIG{SEGV} = \&handle_fatal_signal;
    $SIG{BUS} = \&handle_fatal_signal;
    $SIG{TSTP} = \&handle_fatal_signal;
    $SIG{TTIN} = \&handle_fatal_signal;
    $SIG{TTOU} = \&handle_fatal_signal;
    $SIG{__WARN__} = \&handle_warning;

    # Environment modifications
    $ENV{MAKE} = "make";  # Make sure we don't parallel build, because it doesn't work.
    $ENV{CFLAGS} = "-O2 -march=pentium";  # A bit of optimization, but not much.
    $ENV{LD_LIBRARY_PATH} = "";  # Make glibc happy
    $ENV{PATH} = "/usr/build/bin:/usr/local/build/bin:/usr/lib/qt-1.45/bin:/usr/lib/qt-2.1.0/bin"
                  . ":/bin:/usr/bin:/sbin:/usr/sbin:/usr/local/bin:/usr/X11R6/bin:/usr/kerberos/sbin"
                  . ":/usr/kerberos/bin:.";

    if ($mode eq "build") {
        # Parse the spec file.  This is just for testing right now
        &parse_spec_file($specfile);
    } elsif ($mode eq "explode") {
    } elsif ($mode eq "contents") {
    }

    close(LOGFILE) if ($GLOBAL_LOG);
    return PKGTOOL_SUCCESS;
}

exit &main();
